dnl
dnl Clipman plugin - Clipboards manager for the Xfce4 panel
dnl
dnl 2005      Eduard Roccatello <eduard@xfce.org>
dnl 2006-2007 Nick Schermer <nick@xfce.org>
dnl 2008-2012 Mike Massonnet <mmassonnet@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([project_version_major], [1])
m4_define([project_version_minor], [2])
m4_define([project_version_micro], [3])
m4_define([project_version_build], [@REVISION@])
m4_define([project_version_tag], []) # leave empty for release
m4_define([project_version], [project_version_major().project_version_minor().project_version_micro()ifelse(project_version_tag(), [git], [project_version_tag()-project_version_build()], [project_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2005-2012
        The Xfce development team. All rights reserved.])
AC_INIT([Clipman], [project_version], [http://bugzilla.xfce.org/], [xfce4-clipman-plugin],
       	[http://goodies.xfce.org/projects/panel-plugins/xfce4-clipman-plugin])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_REVISION([project_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_INTLTOOL()

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_LIBX11_REQUIRE()
XDT_CHECK_PACKAGE([EXO], [exo-1], [0.6.0])
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.18.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.18.0])
XDT_CHECK_PACKAGE([GDKX], [gdk-x11-2.0], [2.14.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-2.0], [2.14.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.8.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-1], [4.8.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-1.0], [4.8.0])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.8.0])
XDT_CHECK_PACKAGE([LIBXPROTO], [xproto], [7.0.0])
XDT_CHECK_PACKAGE([LIBXTST], [xtst], [1.0.0])

dnl ***************************
dnl *** Check for libunique ***
dnl ***************************
XDT_CHECK_OPTIONAL_PACKAGE([UNIQUE], [unique-1.0], [1.0.0], unique, [Unique support])
if test x"$UNIQUE_FOUND" = x"yes"; then
	enable_unique=yes
else
	enable_unique=no
fi

dnl *******************************
dnl *** Check for documentation ***
dnl *******************************
AC_ARG_ENABLE([gen-doc], [AC_HELP_STRING([--enable-gen-doc],
              [Generate HTML documentation (requires gnome-doc-utils) [default=no]])],,
              [enable_gen_doc=no])
if test x"$enable_gen_doc" = x"yes"; then
  XDT_CHECK_PACKAGE([GNOME_DOC_UTILS], [gnome-doc-utils], [0.19.2],
  [
    AC_PATH_PROG([ITSTOOL], [itstool], [no])
    if test x"$ITSTOOL" = x"no"; then
        enable_gen_doc=no
    fi

    AC_PATH_PROG([GNOME_DOC_TOOL], [gnome-doc-tool], [no])
    if test x"$GNOME_DOC_TOOL" = x"no"; then
        enable_gen_doc=no
    fi
  ],
  [
    enable_gen_doc=no
  ])

  if test x"$enable_gen_doc" = x"no"; then
    AC_MSG_ERROR([Compiling with --enable-gen-doc but no suitable gnome-doc-utils and/or itstool found.])
  fi
fi
AM_CONDITIONAL([GENERATE_DOCUMENTATION], [test x"$enable_gen_doc" = x"yes"])

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

dnl ************************
dnl *** Makefile outputs ***
dnl ************************
AC_OUTPUT([
Makefile
icons/Makefile
icons/16x16/Makefile
icons/22x22/Makefile
icons/24x24/Makefile
icons/32x32/Makefile
icons/scalable/Makefile
x11-clipboard-manager/Makefile
panel-plugin/Makefile
tests/Makefile
po/Makefile.in
doc/Makefile
doc/media/Makefile
doc/po/Makefile
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "    * Debug Support:    $enable_debug"
echo "    * Unique:           $enable_unique"
echo
if test "x$USE_MAINTAINER_MODE" = "xyes" ; then
echo "Enable Documentation: $enable_gen_doc"
echo
fi
