dnl
dnl Clipman plugin - Clipboards manager for the Xfce4 panel
dnl
dnl 2005      Eduard Roccatello <eduard@xfce.org>
dnl 2006-2007 Nick Schermer <nick@xfce.org>
dnl 2008-2012 Mike Massonnet <mmassonnet@xfce.org>
dnl

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([project_version_major], [1])
m4_define([project_version_minor], [6])
m4_define([project_version_micro], [1])
m4_define([project_version_build], [@REVISION@])
m4_define([project_version_tag], []) # leave empty for release
m4_define([project_version], [project_version_major().project_version_minor().project_version_micro()ifelse(project_version_tag(), [git], [project_version_tag()-project_version_build()], [project_version_tag()])])

dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2005-2016
        The Xfce development team. All rights reserved.])
AC_INIT([Clipman], [project_version], [http://bugzilla.xfce.org/], [xfce4-clipman-plugin],
        [https://docs.xfce.org/panel-plugins/xfce4-clipman-plugin])
AC_CONFIG_HEADERS([config.h])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_REVISION([project_version_build])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 no-dist-gzip dist-bzip2])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl **************************
dnl *** Initialize libtool ***
dnl **************************
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
AM_PROG_CC_C_O()
AC_PROG_LD()
AC_PROG_INSTALL()
AC_PROG_LN_S()
IT_PROG_INTLTOOL([0.35.0])

dnl **********************************
dnl *** Check for standard headers ***
dnl **********************************
AC_HEADER_STDC()

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_LIBX11_REQUIRE()
XDT_CHECK_PACKAGE([GIO], [gio-2.0], [2.32.0])
XDT_CHECK_PACKAGE([GLIB], [glib-2.0], [2.32.0])
XDT_CHECK_PACKAGE([GDKX], [gdk-x11-3.0], [3.14.0])
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.14.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.10.0])
XDT_CHECK_PACKAGE([LIBXPROTO], [xproto], [7.0.0])
XDT_CHECK_PACKAGE([LIBXTST], [xtst], [1.0.0])

dnl *****************************
dnl *** Check for libqrencode ***
dnl *****************************
XDT_CHECK_OPTIONAL_PACKAGE([QRENCODE], [libqrencode], [3.3.0], libqrencode, [QR Code support])
if test x"$QRENCODE_FOUND" = x"yes"; then
	enable_qrencode=yes
else
	enable_qrencode=no
fi

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG()

dnl ************************
dnl *** Makefile outputs ***
dnl ************************
AC_OUTPUT([
Makefile
data/Makefile
data/appdata/Makefile
data/icons/Makefile
data/icons/16x16/Makefile
data/icons/24x24/Makefile
data/icons/32x32/Makefile
data/icons/48x48/Makefile
data/icons/64x64/Makefile
data/icons/scalable/Makefile
x11-clipboard-manager/Makefile
panel-plugin/Makefile
tests/Makefile
po/Makefile.in
])

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "    * Debug Support:    $enable_debug"
echo "    * QR Code:          $enable_qrencode"
echo
